<!DOCTYPE html>
<html>
<head>
<meta charset="UTF-8">
<meta name="author" content="Eric Zong">
<title>程序编写</title>
<link type="text/css" rel="stylesheet" href="../css/common.css"  />
<link type="text/css" rel="stylesheet" href="../css/print.css" media="print" />
<link type="text/css" rel="stylesheet" href="../css/title.css"  />
<link type="text/css" rel="stylesheet" href="../css/tree.css" />
<link type="text/css" rel="stylesheet" href="../css/highlight.css" />

<script type="text/javascript" src="../js/title.js"></script>
<script type="text/javascript" src="../js/highlight.pack.js"></script>
<script type="text/javascript">hljs.initHighlightingOnLoad();</script>
</head>
<body>
  <h1>概述</h1>
  
  <h1>语法</h1>
  <h2>变量和常量</h2>
  <p>用户使用的所有变量和常量都必须在程序块中的声明部分定义。</p>
  <p>在声明变量时，可以使用 default 关键字和 not null 属性，表示变量不允许空。</p>
  <p>变量和常量作用域是用户能够引用变量名称标识符的程序块。</p>
  <p class="next">使用 %type 和 %rowtype 的原因：</p>
  <ul class="last">
    <li>不需要在声明变量时知道数据类型</li>
    <li>如果正在引用的变量的数据类型发生改变，用户 %type 和 %rowtype 变量也会在运行时改变，而不必重新声明变量</li>
  </ul>
  <p class="next">定义记录类型</p>
  <pre class="code"><code>
type record_name is record (
  field_declaration, 
  ... );</code></pre>
  
  <h2>语句块</h2>
  <p>条件是可以取值为 true、false 或 null 的表达式。如果条件取值为 null，将产生异常。</p>
  <p>程序块如果什么都不做可以用 null 关键字表示。在 PL/SQL 代码的可执行部分中，如无包括 null 关键字或其它可执行内容，则将会导致编译错误的发生。</p>
  
  <h1>控制</h1>
  <h2>循环</h2>
  <p>循环是一种程序控制结构，可以使用它来遍历一系列 PL/SQL 语句，并且在 0 次和无限次之间执行这些语句。</p>
  <p>最基本的循环称为无限制循环。loop ... end loop</p>
  <p>无限制是指如果没有 exit 语句，循环将一直运行下去。</p>
  <p class="next">退出循环的方式：</p>
  <ol class="last">
    <li>通过判断来决定是否退出循环</li>
    <li>使用 exit when 语句</li>
  </ol>
  <p>while 循环：while ... loop ... end loop</p>
  
  <h1>事务</h1>
  
  <h2>子事务自治</h2>
  <pre class="code"><code>
-- 子程序自治事务
pragma autonomous_transaction;
-- 示例
create or replace procedure sub_block is  
pragma autonomous_transaction;  
begin
...</code></pre>

  <h1>动态 SQL</h1>
  
  <h2>绑定变量</h2>
  
  <h3>into/using 绑定</h3>
  
  <p>绑定变量需要使用 into、using 关键字。into 用于为输出参数赋值；using 指定输入参数，SQL 字符串中用“:name”引用。</p>
  <pre class="code"><code>
set serveroutput on;  
  
declare
  incoming date:=sysdate-10;
  outgoing int;

begin
 execute immediate 'select count(*) from user_objects where created > :incoming' into outgoing using incoming;
 dbms_output.put_line('count is:' || outgoing);
 
end;
</code></pre>
  <p>into 绑定的输出参数类型应该与 SQL 中的选择列表中的数据类型一一对应。</p>
  <p>使用 using 绑定输入参数的好处之一便是不用进行繁琐的类型转换操作。</p>
  
  <h3>游标绑定</h3>
  <pre class="code"><code>
declare 
  v_cursor number; --定义游标
  v_string varchar2(4000); 
  v_row number; 
begin 
  v_string := 'insert into t_object(a) select count(*) from user_objects where created > :incoming';
 
  v_cursor:=dbms_sql.open_cursor;--打开处理游标 
  dbms_sql.parse(v_cursor,v_string,dbms_sql.native);--解释语句 
  dbms_sql.bind_variable(v_cursor,':incoming',sysdate-30); --给变量赋值 
  v_row := dbms_sql.execute(v_cursor);--执行语句 
  dbms_sql.close_cursor(v_cursor);--关闭游标 
  --dbms_output.put_line(v_row); 
  
  commit; 
  
exception 
  when others then 
    dbms_sql.close_cursor(v_cursor); --关闭游标 
    rollback; 
end; </code></pre>

  <h1>函数</h1>
  
  <h2>可变数组</h2>
  
  <p>通常，使用嵌套表来实现可变数组。</p>
  <p>比如要实现一个字符串数组。</p>
  <p>先声明一个数据记录对象类型，用以存储行数据。</p>
  <pre class="code"><code>
create or replace type stringobject as object
(
  X_ number,
  Y_ varchar2(4000)
)</code></pre>
  <p>再声明对应的嵌套表类型，作为数组类型。</p>
  <pre class="code"><code>
create or replace type stringarray as table of stringobject;</code></pre>
  <p>stringarray 即为定义的数组类型了，其中 X_ 代表索引，Y_ 代表字符串元素。</p>
  <p>最常见的操作便是：定义一个函数，将指定分隔符分隔的字符串分解为字符串数组。下面以管道方式编写该操作函数。</p>
  <pre class="code"><code>
create or replace function split(p_string varchar2, p_seperator varchar2 default ',', p_null_value varchar2 default ' ')
  return stringarray
  pipelined as
  r_array stringobject := stringobject(null, null);
  v_idx integer;
  v_no integer := 1;
  v_str varchar2(4000) := p_string;
  v_seperator_length integer := length(p_seperator);
begin
  if instr(v_str, p_seperator, 1, 1) = 1 then
    v_str := p_null_value || v_str;
  end if;  
  
  if instr(v_str, p_seperator, -1, 1) + v_seperator_length - 1 = length(v_str) then
    v_str := v_str || p_null_value;
  end if;

  loop
    v_idx := instr(v_str, p_seperator);
    exit when v_idx = 0;
    
    r_array.x_ := v_no;
    r_array.y_ := substr(v_str, 1, v_idx - 1);
    v_str := substr(v_str, v_idx + v_seperator_length);
    pipe row(r_array);
    v_no := v_no + 1;
  end loop;
  r_array.x_ := v_no;
  r_array.y_ := v_str;
  pipe row(r_array);
  return;
end split;</code></pre>
  <div class="ext">
    注意：以管道方式定义的函数是以“pipelined as”开始声明部分的，而非“is”；
    并使用“pipe row()”输出行数据；最后只以“return”结束，而不指定返回的值。
  </div>
  <p>利用该函数我们可以使用如下查询语句将字符串分解为数组。</p>
  <pre class="code"><code>
SELECT * FROM TABLE(SPLIT('TOTAL;BIN0;BIN1;BIN2', ';'));</code></pre>
  <p>这将得到如下的结果：</p>
  <table>
    <tr>
      <th>X_</th>
      <th>Y_</th>
    </tr>
    <tr>
      <td>1</td>
      <td>TOTAL</td>
    </tr>
    <tr>
      <td>2</td>
      <td>BIN0</td>
    </tr>
    <tr>
      <td>3</td>
      <td>BIN1</td>
    </tr>
    <tr>
      <td>4</td>
      <td>BIN2</td>
    </tr>
  </table>
  <p>需要注意的是，这种使用管道定义的函数被称为“表函数（table function）”，可以被用于查询中，
  但不能用于 PL/SQL 程序中，即 PL/SQL 程序中不能将该函数的返回值赋值给数组变量。</p>
  <pre class="code"><code>
v_strs := SPLIT('TOTAL;BIN0;BIN1;BIN2', ';'); -- 这行代码会引起编译错误</code></pre>
  <p>如果需要在 PL/SQL 程序中使用该种函数，则应在“select into”中使用，或者干脆不要使用管道方式定义。</p>
  <pre class="code"><code>
CREATE OR REPLACE FUNCTION splitstr(p_string varchar2, p_seperator varchar2 default ',', p_null_value varchar2 default ' ')
   RETURN stringarray
IS
  v_idx integer;
  v_str varchar2(4000) := p_string;
  v_seperator_length integer := length(p_seperator);
  v_strs stringarray := stringarray();
BEGIN
  if instr(v_str, p_seperator, 1, 1) = 1 then
    v_str := p_null_value || v_str;
  end if;  
  
  if instr(v_str, p_seperator, -1, 1) + v_seperator_length - 1 = length(v_str) then
    v_str := v_str || p_null_value;
  end if;
  
  loop
    v_idx := instr(v_str, p_seperator);
    exit when v_idx = 0;
    
    v_strs.extend;
    v_strs(v_strs.count) := stringobject(v_strs.count, substr(v_str, 1, v_idx - 1));
    v_str := substr(v_str, v_idx + v_seperator_length);
    
  end loop;
  
  v_strs.extend;
  v_strs(v_strs.count) := stringobject(v_strs.count, v_str);

  RETURN v_strs;
END splitstr;</code></pre>
  <div class="ext">
    注意：数组对象的声明；行的扩展；行数据的构造等。
  </div>
  
  <h1>附录</h1>
  <p>查询 PL/SQL 和 SQL 中的保留字清单：help reserved words</p>
</body>
</html>